import std.os.Win32;
import std.Mem;
import std.lang.String;
import std.io.InStream;

/*
 * Stream to read from standard input.
 */
in := InStream(Win32.GetStdHandle(-10));

writeToHandle(handle :void*, msg :c8*, length :u32) {
	numberOfBytesWritten :u32;
	Win32.WriteFile(handle, msg, length, &numberOfBytesWritten, null);
}

print(msg :c8*, length :u32) {
	writeToHandle(Win32.GetStdHandle(-11), msg, length);
}

println(msg :c8*) {
	print(msg);
	println();
}

println() {
	ln :c8 = '\n';
	print(&ln, 1);
}

print(msg :c8*) {
	print(msg, Mem.strlen(msg));
}

print(msg :String) {
	print(msg.buffer, msg.length);
}

println(msg :String) {
	print(msg);
	println();
}

print(i :i32) {
	print(String.from(i));
}

println(i :i32) {
	print(i);
	println();
}

/*
 * Stops the current application from running.
 */
exit(exitCode :u32) {
	Win32.ExitProcess(exitCode);
}