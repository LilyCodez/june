import std.os.Win32;
import std.os.Unix;
import std.Mem;
import std.lang.String;
import std.io.InStream;

/*
 * Stream to read from standard input.
 */
#if OS_WINDOWS
stdin := InStream(Win32.GetStdHandle(-10));
#endif
#if OS_UNIX
stdin := InStream(0);
#endif

#if OS_WINDOWS
writeToHandle(handle :void*, msg :c8*, length :u32) {
	numberOfBytesWritten :u32;
	Win32.WriteFile(handle, msg, length, &numberOfBytesWritten, null);
}
#endif

#if OS_UNIX
writeToHandle(handle :i32, msg :c8*, length :u32) {
	Unix.write(handle, msg, length);
}
#endif


// Functions for writing to std out.
//----------------------------------------------

print(msg :c8*, length :u32) {
#if OS_WINDOWS
	writeToHandle(Win32.GetStdHandle(-11), msg, length);
#endif
#if OS_UNIX
	writeToHandle(1, msg, length);
#endif
}

print(msg :c8*) {
	print(msg, Mem.strlen(msg));
}

print(msg :String) {
	print(msg.buffer, msg.length);
}

print(i :i32) {
	print(String.from(i));
}

println() {
	ln :c8 = '\n';
	print(&ln, 1);
}

println<T>(v :T) {
	print(v);
	println();
}

// Explicit implementation to prevent
// array based character templating.
println(v :c8*) {
	print(v);
	println();
}

/*
 * Runs at startup to ensure
 * certain functionality is ran
 * right as the application begins.
 */
initialize() {
#if OS_WINDOWS
	Win32.QueryPerformanceFrequency(&performanceFrequency);
#endif
}

/*
 * Stops the current application from running.
 */
exit(exitCode :u32) {
#if OS_WINDOWS
	Win32.ExitProcess(exitCode);
#endif
#if OS_UNIX
	Unix._exit(exitCode);
#endif
}


// Used to keep track of time
#if OS_WINDOWS
performanceFrequency :i64;
#endif

currentNanoTime() -> i64 {
#if OS_WINDOWS
	current :i64;
	Win32.QueryPerformanceCounter(&current);
	return cast(i64) ((cast(f64) current/performanceFrequency) * 1E+9);
#endif
#if OS_UNIX
	return 0; // TODO: implement!
#endif
}